/* For convenience, define gamma in terms of v: */
gamma:1/sqrt(1-v*v);
/* Define Lx as L(x-hat), Lmx as L(-x-hat), etc. Each
   is represented by a matrix: */
Lx:matrix([gamma,   gamma*v,  0],
         [gamma*v,  gamma,    0],
         [0,        0,        1]);
Ly:matrix([gamma,   0,        gamma*v], 
         [0,        1,        0],
         [gamma*v,  0,        gamma]);
Lmx:matrix([gamma,  -gamma*v, 0],  
         [-gamma*v, gamma,    0],
         [0,        0,        1]);
Lmy:matrix([gamma,  0,        -gamma*v],  
         [0,        1,        0],
         [-gamma*v, 0,        gamma]);
/* Calculate the product of the four matrices: */
T:Lx.Ly.Lmx.Lmy;
/* Define a point on the same light-cone as the origin,
   which is equivalent to defining a direction.
   This is Maxima's notation for the column vector
   (1,1,0): */
P:matrix([1],[1],[0]);
/* Find the result of T acting on this direction,
   expressed as a Taylor series to second order in v: */
taylor(T.P,v,0,2);
